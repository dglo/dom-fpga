TITLE "control message, header-bytes decoder, K.-H. Sulanke,DESY Zeuthen, 2004-04-12";
-- according Arthur Jones protocol

SUBDESIGN dc_hdr_dec
(
	clk				: INPUT;
	reset			: INPUT;
	data_stb		: INPUT;
	data[7..0]		: INPUT;
	msg_type_ena	: INPUT;
	cmd_ena			: INPUT;
	A_nB		    : INPUT;
	ctrl_rcvd		: INPUT;	--  CRC-error-free control message received
	
	ctrl_msg	    : OUTPUT;	
	data_msg	    : OUTPUT;
	cmd_rec[3..0]	: OUTPUT;
	my_adr			: OUTPUT;
	
	idreq_rcvd		: OUTPUT;
	drreq_rcvd		: OUTPUT;
	comres_rcvd		: OUTPUT;
	bfstat_rcvd		: OUTPUT;
	sysres_rcvd		: OUTPUT;
	tcal_rcvd		: OUTPUT;
	idle_rcvd		: OUTPUT;
)

VARIABLE
	ctrl_msg		: DFFE;
	data_msg		: DFFE;
	cmd_rec[3..0]	: DFFE;
	dom_rcvd	    : DFFE;
	
	idreq_rcvd		: DFF;
	drreq_rcvd		: DFF;
	comres_rcvd		: DFF;
	bfstat_rcvd		: DFF;
	sysres_rcvd		: DFF;
	tcal_rcvd		: DFF;
	idle_rcvd		: DFF;
	
BEGIN

	dom_rcvd.clk    	= clk;
	dom_rcvd.clrn    	= !reset;
	dom_rcvd.ena    	= msg_type_ena & data_stb;	
	dom_rcvd.d			= data[7];
	
	my_adr				= dom_rcvd !$ A_nB;

	ctrl_msg.clk		= clk;
	ctrl_msg.clrn		= !reset;
	ctrl_msg.ena		= msg_type_ena & data_stb;
	ctrl_msg.d			= (data[7] !$ A_nB) & (data[6..4]==6);
	
	data_msg.clk		= clk;
	data_msg.clrn		= !reset;
	data_msg.ena		= msg_type_ena & data_stb;	
	data_msg.d			= (data[7] !$ A_nB) & !((data[6..4]==6) # (data[6..4]==7));
	
	cmd_rec[].clk		= clk;
	cmd_rec[].clrn		= !reset;
	cmd_rec[].ena		= cmd_ena & data_stb;
	cmd_rec[3..0].d		= data[3..0];
	
	idreq_rcvd.(clk, clrn)	= (clk, !reset);
	drreq_rcvd.(clk, clrn)	= (clk, !reset);
	comres_rcvd.(clk, clrn)	= (clk, !reset);
	bfstat_rcvd.(clk, clrn)	= (clk, !reset);
	sysres_rcvd.(clk, clrn)	= (clk, !reset);
	tcal_rcvd.(clk, clrn)	= (clk, !reset);
	idle_rcvd.(clk, clrn)	= (clk, !reset);

	idreq_rcvd.d			= ctrl_msg.q & (cmd_rec[3..0].q==B"0011") & ctrl_rcvd;
	drreq_rcvd.d			= ctrl_msg.q & (cmd_rec[3..0].q==B"0101") & ctrl_rcvd;
	comres_rcvd.d			= ctrl_msg.q & (cmd_rec[3..0].q==B"1011") & ctrl_rcvd;
	bfstat_rcvd.d			= ctrl_msg.q & (cmd_rec[3..0].q==B"1100") & ctrl_rcvd;
	sysres_rcvd.d			= ctrl_msg.q & (cmd_rec[3..0].q==B"1101") & ctrl_rcvd;
	tcal_rcvd.d				= ctrl_msg.q & (cmd_rec[3..0].q==B"1110") & ctrl_rcvd;
	idle_rcvd.d				= ctrl_msg.q & (cmd_rec[3..0].q==B"1111") & ctrl_rcvd;
END;
