TITLE "control message, header-bytes decoder, K.-H. Sulanke,DESY Zeuthen, 2004-07-07";
-- according Arthur Jones protocol
-- including comm. DAC level adaption

SUBDESIGN hdr_decoder
(
	clk				: INPUT;
	reset			: INPUT;
	data_stb		: INPUT;
	data[7..0]		: INPUT;
	msg_type_ena	: INPUT;
	cmd_ena			: INPUT;
	dom_A_sel	    : INPUT;
	dom_B_sel	    : INPUT;
	seq0_byte	    : INPUT;
	seq1_byte	    : INPUT;
	
	ctrl_msg	    : OUTPUT;	
	data_msg	    : OUTPUT;
	cmd_rec[3..0]	: OUTPUT;
	dorlev_up_rqa   : OUTPUT;	-- request from DOM_a to increase the Tx signal
	dorlev_dn_rqa	: OUTPUT;	-- 1 clock length only
	dorlev_up_rqb   : OUTPUT;	-- request from DOM_a to increase the Tx signal
	dorlev_dn_rqb	: OUTPUT;
	domleva[7..0]	: OUTPUT;	-- presently used DAC level by DOM_a
	domlevb[7..0]	: OUTPUT;	
)

VARIABLE
	ctrl_msg		: DFFE;
	data_msg		: DFFE;
	cmd_rec[3..0]	: DFFE;
	domleva[7..0]	: DFFE;
	domlevb[7..0]	: DFFE;
	dorlev_up_rqa   : DFF;	-- request from DOM_a to increase the Tx signal
	dorlev_dn_rqa	: DFF;
	dorlev_up_rqb   : DFF;	-- request from DOM_a to increase the Tx signal
	dorlev_dn_rqb	: DFF;

BEGIN

	ctrl_msg.clk		= clk;
	ctrl_msg.clrn		= !reset;
	ctrl_msg.ena		= msg_type_ena & data_stb;
	ctrl_msg.d			= (data[6..4]==6)  &
							((dom_A_sel &  data[7]) #
							 (dom_B_sel & !data[7]));
	
	data_msg.clk		= clk;
	data_msg.clrn		= !reset;
	data_msg.ena		= msg_type_ena & data_stb;	
	data_msg.d			= !((data[6..4]==6) # (data[6..4]==7)) &
							((dom_A_sel &  data[7]) #
							 (dom_B_sel & !data[7]));
	
	cmd_rec[].clk		= clk;
	cmd_rec[].clrn		= !reset;
	cmd_rec[].ena		= cmd_ena & data_stb;
	cmd_rec[3..0].d		= data[3..0];
	
	dorlev_up_rqa.(clk,clrn)	= (clk, !reset);
	dorlev_dn_rqa.(clk,clrn)	= (clk, !reset);
	dorlev_up_rqb.(clk,clrn)	= (clk, !reset);
	dorlev_dn_rqb.(clk,clrn)	= (clk, !reset);
	
	dorlev_up_rqa.d		= seq1_byte & data[7] & data_stb & dom_A_sel;
	dorlev_dn_rqa.d		= seq1_byte & data[6] & data_stb & dom_A_sel;
	dorlev_up_rqb.d		= seq1_byte & data[7] & data_stb & dom_B_sel;
	dorlev_dn_rqb.d		= seq1_byte & data[6] & data_stb & dom_B_sel;

	domleva[].clk		= clk;
	domleva[].clrn		= !reset;
	domleva[].ena		= seq0_byte & data_stb & dom_A_sel;
	domleva[7..0].d		= data[7..0];
	
	domlevb[].clk		= clk;
	domlevb[].clrn		= !reset;
	domlevb[].ena		= seq0_byte & data_stb & dom_B_sel;
	domlevb[7..0].d		= data[7..0];
END;
