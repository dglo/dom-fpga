TITLE "timer used for DOM-TCAL, K.-H. Sulanke,DESY Zeuthen, 2003/09/11";

INCLUDE "dc_tcal_ct.inc";	-- modulus 20000 15 bit counter

--FUNCTION dc_tcal_ct (	clock,	aclr)
--RETURNS (	q[14..0]);

-- clock = 20MHz / 50ns

CONSTANT DELAY_15us = 300;	-- 15us

OPTIONS	BIT0=LSB;

SUBDESIGN tcal_timer
(
	CCLK			: INPUT;	-- communication clock
	tcal_psnd		: INPUT;	-- TCAL timer start
	timer_clrn		: INPUT;	-- 

	del_15us		: OUTPUT;	-- TCAL can start now
	h_pulse			: OUTPUT;	-- TCAL high pulse time
	l_pulse			: OUTPUT;	-- TCAL low pulse time
	pulse_sent		: OUTPUT;	-- whole TCAL pulse sent
	tx_time_lat		: OUTPUT;	-- to latch the start time time of the TCAL pulse
)
VARIABLE
	del_15us		: DFF;
	h_pulse			: DFF;
	l_pulse			: DFF;
	pulse_sent		: DFF;
	tx_time_lat		: DFF;
	timer			: dc_tcal_ct;	-- modulus 20000 15 bit counter

BEGIN

timer.clock		= CCLK;
timer.aclr		= !timer_clrn;

del_15us.(clk, clrn)	= (CCLK, timer_clrn);
h_pulse.(clk, clrn)		= (CCLK, timer_clrn);
l_pulse.(clk, clrn)		= (CCLK, timer_clrn);
pulse_sent.(clk, clrn)	= (CCLK, timer_clrn);
tx_time_lat.(clk, clrn)	= (CCLK, timer_clrn);

del_15us.d		= (timer.q[]==DELAY_15us);		-- 15us delay

tx_time_lat.d	= (timer.q[]==(DELAY_15us+2)) & tcal_psnd;

h_pulse.d		= (timer.q[]==(DELAY_15us+2)) & tcal_psnd
				# h_pulse.q & !(timer.q[]==(DELAY_15us+22));
l_pulse.d		= (timer.q[]==(DELAY_15us+22)) & tcal_psnd
				# l_pulse.q & !(timer.q[]==(DELAY_15us+42));
				
pulse_sent.d	= (timer.q[]==(DELAY_15us+62)) & tcal_psnd;	-- 1us later

END;
